plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'maven-publish'
    id "com.diffplug.spotless" version "5.12.4"
}

group 'io.txcl.healpix'
version '4.0.1'
sourceCompatibility = 11

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    implementation 'org.opencadc:nom-tam-fits:1.16.3'
    testImplementation 'junit:junit:4.13.1'
}

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat('1.6')
        removeUnusedImports()
        replaceRegex 'Bug in google format javadoc: https://github.com/google/google-java-format/issues/254', '  \\* (<p>)+', '  * <p>'
    }
}


// Display is mainly for CI
test {
    afterTest { desc, result ->
        logger.quiet "Executed test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Exclosure/healpix-java")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}